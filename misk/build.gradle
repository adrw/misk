import org.junit.platform.console.options.Details
import groovy.json.JsonSlurper

apply plugin: 'kotlin'
kotlin {
  experimental {
    coroutines 'enable'
  }
}
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: "com.palantir.docker-run"

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
    allWarningsAsErrors = true
  }
}
compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
    allWarningsAsErrors = true
  }
}

sourceSets {
  main.java.srcDirs += 'src/main/kotlin/'
  test.java.srcDirs += 'src/test/kotlin/'
  main.resources {
    srcDirs += [
      'web/@misk/common/lib',
      'web/@misk/components/lib',
      'web/@misk/dev/lib',
      'web/@misk/tslint/lib',
      'web/tabs/config/lib',
      'web/tabs/example/lib',
      'web/tabs/loader/lib'
    ]
    exclude '**/node_modules'
  }
}

junitPlatform {
  details Details.VERBOSE
}

dependencies {
  compile dep.kotlinStdLib
  compile dep.kotlinTest
  compile dep.bouncycastle
  compile dep.guava
  compile dep.guice
  compile dep.guiceMultibindings
  compile dep.javaxInject
  compile dep.okHttp
  compile dep.okio
  compile dep.kotlinReflection
  compile dep.moshi
  compile dep.conscrypt
  compile dep.jettyAlpnConscypt
  compile dep.jettyHttp2
  compile dep.jettyServer
  compile dep.servletApi
  compile dep.jettyServlet
  compile dep.jettyWebsocketServlet
  compile dep.jettyWebsocketServer
  compile dep.kubernetesClient
  compile dep.wireRuntime
  compile dep.loggingApi
  compile dep.jacksonDatabind
  compile dep.jacksonDataformatYaml
  compile dep.jacksonKotlin
  compile dep.jacksonJsr310
  compile dep.jCommander
  compile dep.openTracing
  compile dep.openTracingUtil
  compile dep.openTracingOkHttp
  compile dep.retrofit
  compile dep.retrofitMoshi
  compile dep.retrofitWire
  compile dep.logbackClassic
  compile dep.slf4jApi
  compile dep.jaxbApi
  compile dep.prometheusClient
  compile dep.prometheusHotspot

  testCompile dep.kotlinxCoroutines
  testCompile dep.mockitoCore
  testCompile project(':misk-testing')
}

ext.generateDockerContainerName = { service, project, task ->
  return "${new Date().format("YMD-HMS")}-${service}-${project}-${task}"
}

ext.getPackageJsonPort = { path ->
  def packageFile= new File("${project.projectDir}/web/${path}/package.json")
  def packageJson = new JsonSlurper().parseText(packageFile.text)
  return packageJson.miskTabWebpack.port
}

task compileWeb(type: Exec) {
  workingDir 'web'
  commandLine 'sh', '-c', "docker run --rm --name ${generateDockerContainerName('misk', 'misk', 'compileWeb')} -v ${project.projectDir}/web:/web squareup/misk-node-build:0.0.1"
}

task developWeb(type: Exec) {
  workingDir 'web'
  if (project.hasProperty("devtabs")) {
    project.devtabs.split(',').each {
      def devContainerName = "${generateDockerContainerName('misk', 'misk', "develop-${it.split('/').join('-')}")}"
      def command = "docker run -d --rm --name ${devContainerName} -v ${project.projectDir}/web/${it}:/web/${it} -p ${getPackageJsonPort(it)}:${getPackageJsonPort(it)} squareup/misk-node-develop:0.0.1"
      println command
      println "See dev server logs at \$ docker logs -f ${devContainerName}"
      println "Manually shut down dev server with \$ docker kill ${devContainerName}"
      commandLine 'sh', '-c', command
    }
  }
}

jar.dependsOn compileWeb

if (rootProject.file("hooks.gradle").exists()) {
  apply from: rootProject.file("hooks.gradle")
}
