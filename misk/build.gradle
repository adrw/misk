import org.junit.platform.console.options.Details

apply plugin: 'kotlin'
kotlin {
  experimental {
    coroutines 'enable'
  }
}
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: "com.vanniktech.maven.publish"

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
    allWarningsAsErrors = true
  }
}
compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
    allWarningsAsErrors = true
  }
}

sourceSets {
  main.java.srcDirs += 'src/main/kotlin/'
  test.java.srcDirs += 'src/test/kotlin/'
}

junitPlatform {
  details Details.VERBOSE
}

dependencies {
  compile dep.kotlinStdLib
  compile dep.kotlinTest
  compile dep.bouncycastle
  compile dep.guava
  compile dep.guice
  compile dep.guiceMultibindings
  compile dep.javaxInject
  compile dep.okHttp
  compile dep.okio
  compile dep.kotlinReflection
  compile dep.moshi
  compile dep.conscrypt
  compile dep.jettyAlpnConscypt
  compile dep.jettyHttp2
  compile dep.jettyServer
  compile dep.servletApi
  compile dep.jettyServlet
  compile dep.jettyWebsocketServlet
  compile dep.jettyWebsocketServer
  compile dep.kubernetesClient
  compile dep.wireRuntime
  compile dep.loggingApi
  compile dep.jacksonDatabind
  compile dep.jacksonDataformatYaml
  compile dep.jacksonKotlin
  compile dep.jacksonJsr310
  compile dep.jCommander
  compile dep.openTracing
  compile dep.openTracingUtil
  compile dep.openTracingOkHttp
  compile dep.retrofit
  compile dep.retrofitMoshi
  compile dep.retrofitWire
  compile dep.logbackClassic
  compile dep.slf4jApi
  compile dep.jaxbApi
  compile dep.prometheusClient
  compile dep.prometheusHotspot

  testCompile dep.kotlinxCoroutines
  testCompile dep.mockitoCore
  testCompile project(':misk-testing')
}

jar {
  into("web/") {
    from("./web/@misk/common/lib/")
    from("./web/@misk/components/dist/")
    from("./web/@misk/dev/")
    from("./web/@misk/tabs/")
    from("./web/@misk/tslint/")
    from("./web/tabs/config/dist/")
    from("./web/tabs/example/dist/")
    from("./web/tabs/loader/dist/")
  }
}

if (rootProject.file("hooks.gradle").exists()) {
  apply from: rootProject.file("hooks.gradle")
}

task compileWebpack {
  doLast {
    def cmd = ['bash', '-c', 'npm config set loglevel warn && npm install && yarn build']
    def webpackProjects = [
            "misk/web/@misk/common",
            "misk/web/@misk/components",
            "misk/web/tabs/config",
            "misk/web/tabs/example",
            "misk/web/tabs/loader",
    ]
    webpackProjects.each { project ->
      println "Compiling Webpack Project... " + project
      println cmd.execute(null, new File(project)).text
    }
  }
}

assemble.dependsOn compileWebpack
